#include <WiFi.h>
#include <WebServer.h>

const char *ssid = "ESP32-AP";       // Nome da rede Wi-Fi (SSID)
const char *password = "12345678";   // Senha da rede Wi-Fi

WebServer server(80);

const int trigPin = 19;   // Pino Trig do sensor HC-SR04
const int echoPin = 18;  // Pino Echo do sensor HC-SR04
const int ledPin = 2;    // Pino onde o LED est√° conectado

long readUltrasonicDistance() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);
  
  long duration = pulseIn(echoPin, HIGH);
  long distance = duration * 0.034 / 2;
  
  return distance;
}

void handleRoot() {
  String html = "<html><head><title>ESP32 Distance Sensor</title></head><body>";
  html += "<h1>Sensor Distance Data</h1>";
  html += "<div id=\"distance\"></div>";
  html += "<script>";
  html += "function fetchData() {";
  html += "  fetch('/distance').then(response => response.text()).then(data => {";
  html += "    document.getElementById('distance').innerHTML = 'Distance: ' + data + ' cm';";
  html += "  });";
  html += "}";
  html += "setInterval(fetchData, 100);"; // Atualiza a cada 500 ms
  html += "</script>";
  html += "</body></html>";
  server.send(200, "text/html", html);
}

void handleDistance() {
  long distance = readUltrasonicDistance();
  server.send(200, "text/plain", String(distance));
}

void setup() {
  Serial.begin(115200);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);

  // Configura o ESP32 como um ponto de acesso
  WiFi.softAP(ssid, password);
  Serial.println("Acess Point.");
  Serial.print("IP: ");
  Serial.println(WiFi.softAPIP());

  // Configura as rotas do servidor web
  server.on("/", handleRoot);
  server.on("/distance", handleDistance);

  // Inicia o servidor web
  server.begin();
  Serial.println("Servidor HTTP iniciado.");
}

void loop() {
  server.handleClient();
}
